module

public import ProvabilityLogic.Kripke.Linearity
public import ProvabilityLogic.LabelledGentzen.GLPoint3.Basic
public import ProvabilityLogic.LabelledGentzen.GL.Kripke

@[expose]
public section

variable {κ : Type u} [Nonempty κ]
         {α : Type v} [DecidableEq α]
         {M : Model κ α}

namespace Model

variable {L : M.LabelMap} {R : Finset LabelRel} {ℓΓ ℓΔ : Finset (LabelledFormula α)}
         {x y z : Label} {A B : Formula α}

lemma validate_labelled_relabel_of_eq {S : LabelledSequent α} (heq : L y = L z) :
  M ⊧ˡ[L] (S.relabel y z) ↔ M ⊧ˡ[L] S := by
  have hL : ∀ a : Label, L (if a = y then z else a) = L a := by
    intro a; by_cases h : a = y <;> simp [h, heq];
  simp only [Model.ValidateLabelled, LabelledSequent.relabel,
    LabelledFormula.relabel, Finset.forall_mem_image, Finset.exists_mem_image, hL];

lemma validate_labelled_lin [M.IsGLPoint3]
  (hxy : (x, y) ∈ R) (hxz : (x, z) ∈ R)
  (h₁ : M ⊧ˡ[L] (insert (y, z) R ⸴ ℓΓ ⟹ˡ ℓΔ))
  (h₂ : M ⊧ˡ[L] (insert (z, y) R ⸴ ℓΓ ⟹ˡ ℓΔ))
  (h₃ : M ⊧ˡ[L] ((R ⸴ ℓΓ ⟹ˡ ℓΔ).relabel y z))
  : M ⊧ˡ[L] (R ⸴ ℓΓ ⟹ˡ ℓΔ) := by
  intro hrel hant;
  rcases Model.linear (hrel (x, y) hxy) (hrel (x, z) hxz) with hyz | heq | hzy;
  · exact h₁ (by rintro p hp; rcases Finset.mem_insert.mp hp with rfl | hp; exacts [hyz, hrel p hp]) hant;
  · exact (validate_labelled_relabel_of_eq heq).mp h₃ hrel hant;
  · exact h₂ (by rintro p hp; rcases Finset.mem_insert.mp hp with rfl | hp; exacts [hzy, hrel p hp]) hant;

end Model


namespace LogicGLPoint3.ProvableLabelledGentzen

namespace Kripke

open Model in
theorem soundness {S : LabelledSequent α} (h : ⊢ˡᵍ[GLPoint3] S) :
  ∀ {κ}, [Nonempty κ] → ∀ M : Model κ α, [M.IsGLPoint3] → ∀ L : M.LabelMap, M ⊧ˡ[L] S := by
  intro κ _ M _;
  induction h with
  | axm x A => exact λ _ => validate_labelled_axm;
  | botL x => exact λ _ => validate_labelled_botL;
  | wkRel _ hR ih => exact λ L => validate_labelled_wkRel (ih L) hR;
  | wkAnt _ hΓ ih => exact λ L => validate_labelled_wkAnt (ih L) hΓ;
  | wkSuc _ hΔ ih => exact λ L => validate_labelled_wkSuc (ih L) hΔ;
  | impL _ _ ih₁ ih₂ => exact λ L => validate_labelled_impL (ih₁ L) (ih₂ L);
  | impR _ ih => exact λ L => validate_labelled_impR (ih L);
  | boxL hxy hxA _ ih => exact λ L => validate_labelled_boxL hxy hxA (ih L);
  | boxRLob hfresh _ ih => exact λ L => validate_labelled_boxRLob hfresh ih;
  | irref hxx => exact λ _ => validate_labelled_irref hxx;
  | trans hxy hyz _ ih => exact λ L => validate_labelled_trans hxy hyz (ih L);
  | lin hxy hxz _ _ _ ih₁ ih₂ ih₃ => exact λ L => validate_labelled_lin hxy hxz (ih₁ L) (ih₂ L) (ih₃ L);

theorem soundness_formula {x : Label} {A : Formula α} (h : ⊢ˡᵍ[GLPoint3] (∅ ⸴ ∅ ⟹ˡ {x ∶ A})) :
  ∀ {κ}, [Nonempty κ] → ∀ M : Model κ α, [M.IsGLPoint3] → M.Validate A := by
  intro κ _ M _ w;
  obtain ⟨ℓA, hlf, hf⟩ := soundness h M (λ _ => w) (by grind) (by grind);
  grind;

end Kripke

end LogicGLPoint3.ProvableLabelledGentzen

end
