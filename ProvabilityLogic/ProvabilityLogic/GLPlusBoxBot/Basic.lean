module

public import ProvabilityLogic.ProvabilityLogic.GL.Basic
public import ProvabilityLogic.Logic.GLPlusBoxBot.Basic

@[expose] public section

open Classical
open FFL
open FFL.FirstOrder.ProvabilityAbstraction

variable {κ : Type*} [Nonempty κ]
         {α : Type*}
         {A B : _root_.Formula α}

namespace LogicGLPlusBoxBot

section

variable {L : FirstOrder.Language} [L.ReferenceableBy L]
         [L.DecidableEq]
         {T U : FirstOrder.Theory L} [Diagonalization T] [T ⪯ U]
         {𝔅 : Provability T U} [𝔅.HBL] {f : Realization α L}

lemma arithmetical_soundness (hA : A ∈ LogicGLPlusBoxBot 𝔅.height) {f : Realization α L} :
    U ⊢ A.interpret f 𝔅 := by
  cases h : 𝔅.height
  case _ =>
    simp [LogicGLPlusBoxBot, h] at hA;
    exact LogicGL.arithmetical_soundness' $ hA;
  case _ n =>
    have : U ⊢ (□^[n]⊥ : Formula α).interpret f 𝔅 🡒 A.interpret f 𝔅 :=
      LogicGL.arithmetical_soundness' $ LogicGLPlusBoxBot.iff_provable_provable_GL.mp $ h ▸ hA;
    apply this ⨀ ?_;
    rw [Formula.interpret_boxItr];
    apply 𝔅.height_le_iff_boxBot.mp;
    simp_all;

end

section

variable {T : FirstOrder.ArithmeticTheory} [T.Δ₁] [𝗜𝚺₁ ⪯ T]
variable {M : RootedModel κ α}

theorem arithmetical_completeness {n : ℕ∞} (hn : n ≤ T.height)
  (h : ∀ f : Realization α ℒₒᵣ, T ⊢ f T A) : A ∈ LogicGLPlusBoxBot n := by
  match n with
  | .none =>
    apply LogicGL.arithmetical_completeness_of_infinity_height (T := T) ?_ h;
    exact eq_top_iff.mpr hn;
  | .some n =>
    apply LogicGLPlusBoxBot.iff_provable_provable_GL.mpr;
    apply LogicGL.arithmetical_completeness_of_finite_le (T := T) ?_ h;
    exact hn;

theorem arithmetical_completeness_iff
  : A ∈ LogicGLPlusBoxBot T.height ↔ (∀ f : Realization α ℒₒᵣ, T ⊢ f T A) := by
  constructor;
  . intro h f; exact arithmetical_soundness h;
  . exact arithmetical_completeness (by simp);

lemma eq_provabilityLogic : LogicGLPlusBoxBot (α := α) T.height = T.provabilityLogic := by
  ext A;
  exact arithmetical_completeness_iff;

end

end LogicGLPlusBoxBot

end
