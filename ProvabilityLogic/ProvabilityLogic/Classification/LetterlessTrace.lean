module

public import ProvabilityLogic.Logic.GLAlpha.Basic
public import ProvabilityLogic.Logic.GLBetaMinus.Basic

@[expose]
public section

namespace LetterlessFormula

@[grind]
def spectrum (A : LetterlessFormula) : Set ℕ := match A with
  | ⊥ => ∅
  | A 🡒 B => (spectrum A)ᶜ ∪ spectrum B
  | □A => { n | ∀ i < n, i ∈ spectrum A }

variable {A B : LetterlessFormula}

lemma spectrum_bot : spectrum (⊥ : LetterlessFormula) = ∅ := by grind;
lemma spectrum_top : spectrum (⊤ : LetterlessFormula) = Set.univ := by grind;
lemma spectrum_imp : spectrum (A 🡒 B) = (spectrum A)ᶜ ∪ spectrum B := by simp [spectrum]
lemma spectrum_neg : spectrum (∼A) = (spectrum A)ᶜ := by simp [spectrum]
lemma spectrum_or  : spectrum (A ⋎ B) = spectrum A ∪ spectrum B := by simp [spectrum];
lemma spectrum_and : spectrum (A ⋏ B) = spectrum A ∩ spectrum B := by simp [spectrum];
lemma spectrum_box : spectrum (□A) = { n | ∀ i < n, i ∈ A.spectrum } := by simp [spectrum];

attribute [simp, grind .]
  spectrum_bot
  spectrum_top
attribute [grind =]
  spectrum_imp
  spectrum_neg
  spectrum_or
  spectrum_and
  spectrum_box

@[simp, grind =]
lemma spectrum_boxItr {n : ℕ} : spectrum (□^[(n + 1)]A) = { k | ∀ i < k, i ∈ spectrum (□^[n]A) } := by
  induction n <;> grind;

@[grind =]
lemma spectrum_boxdot : spectrum (⊡A) = { n | ∀ i ≤ n, i ∈ spectrum A } := by grind;

@[simp, grind =]
lemma spectrum_boxItr_bot : spectrum (□^[n]⊥) = { i | i < n } := by
  induction n with
  | zero => grind;
  | succ n ih =>
    calc
      _ = { i | ∀ k < i, k ∈ spectrum (□^[n]⊥) } := by grind
      _ = { i | ∀ k < i, k < n }                           := by simp [ih];
      _ = { i | i < n + 1 }                                := by grind;

@[simp, grind =]
lemma spectrum_lconj {Γ : FormulaList Empty} : spectrum (⋀Γ) = ⋂ A ∈ Γ, spectrum A := by
  match Γ with
  | [] | [A] | A :: B :: Γ => simp [FormulaList.conj, spectrum_and, spectrum_lconj]

@[simp, grind =]
lemma spectrum_fconj {Γ : FormulaFinset Empty} : spectrum (⋀Γ) = ⋂ A ∈ Γ, spectrum A := by
  simp [FormulaFinset.conj, spectrum_lconj];

@[simp, grind =]
lemma spectrum_TBB : spectrum (TBB n) = {n}ᶜ := by
  rw [TBB, spectrum_imp, spectrum_boxItr_bot, spectrum_boxItr_bot];
  ext i;
  grind;

@[grind]
def trace (A : Formula Empty) := (spectrum A)ᶜ

lemma trace_bot : trace ⊥ = Set.univ := by grind;
lemma trace_top : trace ⊤ = ∅ := by grind;
lemma trace_and : trace (A ⋏ B) = trace A ∪ trace B := by grind;
lemma trace_or  : trace (A ⋎ B) = trace A ∩ trace B := by grind;
lemma trace_imp : trace (A 🡒 B) = (trace A)ᶜ ∩ trace B := by grind;
lemma trace_neg : trace (∼A) = (trace A)ᶜ := by grind;

attribute [simp, grind .]
  trace_bot
  trace_top
attribute [grind =]
  trace_and
  trace_or
  trace_imp
  trace_neg

@[simp, grind =]
lemma trace_TBB : trace (TBB n) = {n} := by grind;

@[simp, grind =]
lemma trace_lconj {Γ : FormulaList Empty} : trace (⋀Γ) = ⋃ A ∈ Γ, trace A := by
  match Γ with
  | [] | [A] | A :: B :: Γ => simp [FormulaList.conj, trace_and, trace_lconj]

@[simp, grind =]
lemma trace_fconj {Γ : FormulaFinset Empty} : trace (⋀Γ) = ⋃ A ∈ Γ, trace A := by
  simp [FormulaFinset.conj, trace_lconj];

@[simp, grind =]
lemma trace_TBBMinus {s : Set ℕ} (hs : s.Finite) : trace (TBBMinus s) = sᶜ := by simp [trace_neg, trace_fconj];

@[grind .]
lemma spectrum_finite_or_cofinite : A.spectrum.Finite ∨ A.spectrumᶜ.Finite := by
  induction A with
  | atom a => grind;
  | bot => grind;
  | imp A B ihA ihB =>
    simp only [spectrum_imp, Set.finite_union];
    rcases ihA with (hA | hA) <;> rcases ihB with (hB | hB);
    · right; rw [Set.compl_union, compl_compl]; exact hA.inter_of_left _;
    · right; rw [Set.compl_union, compl_compl]; exact hA.inter_of_left _;
    · left; exact ⟨hA, hB⟩;
    · right; rw [Set.compl_union, compl_compl]; exact hB.inter_of_right _;
  | box A ih =>
    by_cases h : spectrum A = Set.univ;
    . grind;
    . left;
      obtain ⟨k, hk₁, hk₂⟩ := exists_minimal_of_wellFoundedLT (λ k => k ∉ spectrum A) $ Set.ne_univ_iff_exists_notMem _ |>.mp h;
      have : {n | ∀ i < n, i ∈ spectrum A} = { n | n ≤ k} := by
        ext i;
        suffices (∀ j < i, j ∈ spectrum A) ↔ i ≤ k by simpa [Set.mem_ofPred_eq];
        constructor;
        . intro h;
          contrapose! hk₁;
          exact h k (by omega);
        . intro h j hji;
          contrapose! hk₂;
          use j;
          constructor;
          . assumption;
          . omega;
      rw [spectrum_box, this];
      apply Set.finite_le_nat;

@[grind .]
lemma trace_finite_or_cofinite : (trace A).Finite ∨ (trace A)ᶜ.Finite := by
  simp only [trace, compl_compl];
  exact spectrum_finite_or_cofinite.symm;

@[grind →]
lemma spectrum_finite_of_trace_infinite : (trace A).Infinite → (spectrum A).Finite := by
  rcases spectrum_finite_or_cofinite (A := A) with (h | h);
  . tauto;
  . simp_all [trace];

@[grind →]
lemma trace_finite_of_spectrum_infinite : (spectrum A).Infinite → (trace A).Finite := by
  contrapose!;
  exact spectrum_finite_of_trace_infinite;


end LetterlessFormula


open LetterlessFormula (spectrum trace)


namespace Model

variable
  [Nonempty κ]
  {M : Model κ Empty} [Fintype M.World] [M.IsGL]
  {x : M.World} {A : LetterlessFormula}

lemma iff_forces_rank_mem_spectrum : x ⊩[_] A ↔ x.rank ∈ (spectrum A) := by
  induction A generalizing x with
  | box A ih =>
    calc
      _ ↔ ∀ y, x ≺ y → y ⊩[_] A := by grind;
      _ ↔ ∀ y, x ≺ y → y.rank ∈ (LetterlessFormula.spectrum A) := by simp [ih];
      _ ↔ ∀ i < x.rank, i ∈ (spectrum A) := by
        constructor;
        . intro h i hi;
          grind [of_lt_rank hi];
        . grind [rank_lt_of_rel];
      _ ↔ x.rank ∈ spectrum (□A) := by grind;
  | _ => grind;

lemma iff_not_forces_rank_mem_trace : x ⊮[_] A ↔ x.rank ∈ (trace A) := by
  grind [iff_forces_rank_mem_spectrum];

/--
  Forcing of a lifted letterless formula is determined by the rank
  (generalization of `iff_forces_rank_mem_spectrum` to models over arbitrary `α`).
-/
lemma iff_forces_lift_rank_mem_spectrum
    {α : Type*} {κ : Type*} [Nonempty κ] {M : Model κ α} [Fintype M.World] [M.IsGL]
    {x : M.World} {B : LetterlessFormula} :
    x ⊩[_] (LetterlessFormula.lift B : Formula α) ↔ x.rank ∈ LetterlessFormula.spectrum B := by
  induction B generalizing x with
  | atom a => exact a.elim;
  | bot => simp [LetterlessFormula.lift];
  | imp B C ihB ihC =>
    show ((x ⊩[_] (LetterlessFormula.lift B : Formula α)) → (x ⊩[_] (LetterlessFormula.lift C : Formula α))) ↔ _;
    rw [ihB, ihC, LetterlessFormula.spectrum_imp];
    grind;
  | box B ihB =>
    calc
      _ ↔ ∀ y, x ≺ y → y ⊩[_] (LetterlessFormula.lift B : Formula α) := by
        exact Model.World.forces_box (A := (LetterlessFormula.lift B : Formula α));
      _ ↔ ∀ y, x ≺ y → Model.World.rank y ∈ LetterlessFormula.spectrum B := by simp [ihB];
      _ ↔ ∀ i < x.rank, i ∈ LetterlessFormula.spectrum B := by
        constructor;
        . intro h i hi;
          grind [Model.of_lt_rank hi];
        . grind [Model.rank_lt_of_rel];
      _ ↔ _ := by grind [LetterlessFormula.spectrum_box];

end Model

universe u

section

variable {α : Type u} {A B : LetterlessFormula}

lemma spectrum_TFAE : [
  n ∈ spectrum A,
  ∀ {κ : Type u}, [Nonempty κ] → ∀ M : RootedModel κ α, [Fintype M.World] → [M.IsGL] → M.height = n → M.root.1 ⊩[_] A,
  ∃ κ : Type u, ∃ _ : Nonempty κ, ∃ M : RootedModel κ α, ∃ _ : Fintype M.World, ∃ _ : M.IsGL, M.height = n ∧ M.root.1 ⊩[_] A,
  ∀ {κ : Type 0}, [Nonempty κ] → [Fintype κ] → ∀ M : Model κ Empty, [M.IsGL] → ∀ x : M.World, x.rank = n → x ⊩[_] A,
  ∃ κ : Type 0, ∃ _ : Nonempty κ, ∃ _ : Fintype κ, ∃ M : Model κ Empty, ∃ _ : M.IsGL, ∃ x : M.World, x.rank = n ∧ x ⊩[_] A,
].TFAE := by
  tfae_have 1 → 2 := by grind [Model.iff_forces_lift_rank_mem_spectrum];
  tfae_have 2 → 3 := by
    intro h;
    have e : (finiteLineModel n α).World ≃ ULift.{u} (Fin (n + 1)) := Equiv.ulift.symm;
    have h₁ : ((finiteLineModel n α).reindex e).height = n :=
      RootedModel.height_reindex.trans finiteLineModel.height_eq;
    use ULift.{u} (Fin (n + 1)), inferInstance, (finiteLineModel n α).reindex e, inferInstance,
      inferInstance;
    exact ⟨h₁, h _ h₁⟩;
  tfae_have 3 → 1 := by grind [Model.iff_forces_lift_rank_mem_spectrum];
  tfae_have 1 → 4 := by grind [Model.iff_forces_rank_mem_spectrum];
  tfae_have 4 → 5 := by
    intro h;
    use Fin (n + 1), inferInstance, inferInstance, (finiteLineModel n Empty).toModel, inferInstance,
      (finiteLineModel n Empty).root.1;
    exact ⟨finiteLineModel.height_eq, h _ _ finiteLineModel.height_eq⟩;
  tfae_have 5 → 1 := by grind [Model.iff_forces_rank_mem_spectrum];
  tfae_finish;

namespace LetterlessFormula

variable {n : ℕ}

/-- A letterless formula's trace is characterized by non-forcing on a `RootedModel` over an arbitrary universe and `α`. -/
lemma iff_mem_trace_rootedModel {B : LetterlessFormula} :
  n ∈ LetterlessFormula.trace B ↔
  ∃ κ : Type u, ∃ _ : Nonempty κ, ∃ M : RootedModel κ α, ∃ _ : Fintype M.World, ∃ _ : M.IsGL,
    M.height = n ∧ M.root.1 ⊮[_] (LetterlessFormula.lift B : Formula α) := by
  have h := spectrum_TFAE (n := n) (A := B) (α := α) |>.out 0 1;
  unfold LetterlessFormula.trace;
  rw [Set.mem_compl_iff, h];
  push Not;
  rfl

end LetterlessFormula

lemma iff_GL_proves_spectrum_univ : A ∈ LogicGL ↔ spectrum A = Set.univ := by
  rw [Set.eq_univ_iff_forall];
  apply Iff.trans $ LogicGL.iff_forces;
  constructor;
  . intro h n;
    apply spectrum_TFAE (α := Empty) |>.out 3 0 |>.mp;
    intro κ _ _ M _ x rfl;
    have : Finite M.World := by infer_instance;
    apply @h κ _ M {};
  . intro h κ _ M _ x;
    have : Fintype M.World := Fintype.ofFinite _;
    have := spectrum_TFAE (α := Empty) |>.out 0 3 |>.mp $ h x.rank;
    exact this M x rfl;

lemma iff_GL_proves_imp_GL_subset_spectrum : (A 🡒 B) ∈ LogicGL ↔ spectrum A ⊆ spectrum B := by
  apply Iff.trans iff_GL_proves_spectrum_univ;
  simp only [LetterlessFormula.spectrum_imp, Set.eq_univ_iff_forall, Set.mem_union, Set.mem_compl_iff];
  grind;

lemma iff_GL_proves_iff_GL_subset_spectrum : (A 🡘 B) ∈ LogicGL ↔ spectrum A = spectrum B := by
  suffices (A 🡘 B) ∈ LogicGL ↔ (A 🡒 B) ∈ LogicGL ∧ (B 🡒 A) ∈ LogicGL by
    grind [Set.Subset.antisymm_iff, iff_GL_proves_imp_GL_subset_spectrum];
  constructor;
  . intro h;
    refine ⟨?_, ?_⟩ <;>
      apply LogicGL.iff_forces_root.mpr <;>
      intro κ _ M _ <;>
      have := LogicGL.iff_forces_root.mp h M <;>
      grind;
  . rintro ⟨h₁, h₂⟩;
    apply LogicGL.iff_forces_root.mpr;
    intro κ _ M _;
    have := LogicGL.iff_forces_root.mp h₁ M;
    have := LogicGL.iff_forces_root.mp h₂ M;
    grind;

lemma LetterlessFormula.TBB_normalization_of_finite_trace (h : (trace A).Finite) : (A 🡘 ⋀(h.toFinset.image TBB)) ∈ LogicGL := by
  apply iff_GL_proves_iff_GL_subset_spectrum.mpr;
  calc
    _ = ⋂ i ∈ trace A, spectrum (TBB i) := by
      ext i;
      simp [LetterlessFormula.trace];
      grind;
    _ = _ := by
      simp [LetterlessFormula.spectrum_fconj];

lemma LetterlessFormula.TBBMinus_normalization_of_finite_spectrum (h : (spectrum A).Finite) : (A 🡘 TBBMinus _ h) ∈ LogicGL := by
  apply iff_GL_proves_iff_GL_subset_spectrum.mpr;
  exact (compl_inj_iff.mp (LetterlessFormula.trace_TBBMinus (s := A.spectrum) h)).symm;

lemma GL_proves_letterless_axiomWeakPoint3 : ((□((⊡A) 🡒 B)) ⋎ (□((⊡B) 🡒 A))) ∈ LogicGL := by
  apply iff_GL_proves_spectrum_univ.mpr;
  grind;

end

open FFL
open FFL.FirstOrder.ProvabilityAbstraction
open LogicGL

namespace LetterlessFormula

section

variable {L : FirstOrder.Language} [L.ReferenceableBy L] {T₀ T : FirstOrder.Theory L} {𝔅 : Provability T₀ T}

@[grind]
def interpret (𝔅 : Provability T₀ T) : LetterlessFormula → FirstOrder.Sentence L
  | ⊥     => ⊥
  | A 🡒 B => (interpret 𝔅 A) 🡒 (interpret 𝔅 B)
  | □A    => 𝔅 (interpret 𝔅 A)

@[grind =]
lemma interpret_boxItr : interpret 𝔅 (□^[n]A) = 𝔅^[n] (interpret 𝔅 A) := by
  induction n with
  | zero => simp [Formula.boxItr];
  | succ n ih => rw [Function.iterate_succ']; grind;

noncomputable abbrev standardInterpret (T : FirstOrder.ArithmeticTheory) [T.Δ₁] := interpret T.standardProvability

lemma interpret_lift {α : Type*} {f : Realization α L} {A : LetterlessFormula} :
    Formula.interpret f 𝔅 (LetterlessFormula.lift A) = LetterlessFormula.interpret 𝔅 A := by
  induction A with
  | atom a => exact a.elim
  | _ => simp_all [Formula.interpret, LetterlessFormula.interpret, LetterlessFormula.lift]

end


variable {T₀ T U : FirstOrder.ArithmeticTheory} [T.Δ₁] {A B : LetterlessFormula}

@[grind]
def Regular (T : FirstOrder.ArithmeticTheory) [T.Δ₁] (A : LetterlessFormula) := ℕ↓[ℒₒᵣ] ⊧ A.interpret T.standardProvability

@[simp, grind .]lemma regular_bot : ¬(Regular T ⊥) := by grind;
@[simp, grind .]lemma regular_top : Regular T ⊤ := by grind;
@[grind =] lemma regular_imp : Regular T (A 🡒 B) ↔ (Regular T A → Regular T B) := by grind;
@[grind =] lemma regular_and : Regular T (A ⋏ B) ↔ (Regular T A ∧ Regular T B) := by grind;
@[grind =] lemma regular_or  : Regular T (A ⋎ B) ↔ (Regular T A ∨ Regular T B) := by grind;
@[grind =] lemma regular_neg : Regular T (∼A) ↔ ¬(Regular T A) := by grind;
@[grind =] lemma regular_iff : Regular T (A 🡘 B) ↔ (Regular T A ↔ Regular T B) := by grind;

@[grind =]
lemma regular_lconj {Γ : LetterlessFormulaList} : Regular T (⋀Γ) ↔ ∀ A ∈ Γ, Regular T A := by
  match Γ with
  | [] | [A] | A :: B :: Γ => simp [FormulaList.conj, regular_and, regular_lconj]

@[grind =]
lemma regular_fconj {Γ : LetterlessFormulaFinset} : Regular T (⋀Γ) ↔ ∀ A ∈ Γ, Regular T A := by
  simp [FormulaFinset.conj, regular_lconj];

@[grind]
def Singular (T : FirstOrder.ArithmeticTheory) [T.Δ₁] (A : LetterlessFormula) := ¬(A.Regular T)

@[simp, grind .] lemma singular_bot : Singular T ⊥ := by grind;
@[simp, grind .] lemma singular_top : ¬(Singular T ⊤) := by grind;
@[grind =] lemma singular_imp : Singular T (A 🡒 B) ↔ (¬Singular T A ∧ Singular T B) := by grind;
@[grind =] lemma singular_and : Singular T (A ⋏ B) ↔ (Singular T A ∨ Singular T B) := by grind;
@[grind =] lemma singular_or  : Singular T (A ⋎ B) ↔ (Singular T A ∧ Singular T B) := by grind;
@[grind =] lemma singular_neg : Singular T (∼A) ↔ ¬(Singular T A) := by grind;

variable [ℕ↓[ℒₒᵣ] ⊧* T]
variable {n : ℕ}

@[grind .]
lemma singular_boxItr_bot : Singular T (□^[n]⊥) := by
  match n with
  | 0 => grind;
  | n + 1 =>
    apply not_imp_not.mpr $ Provability.SoundOn.sound_on;
    rw [interpret_boxItr];
    exact FFL.FirstOrder.ProvabilityAbstraction.iIncon_unprovable_of_sigma1_sound n;

@[simp, grind .]
lemma regular_TBB : Regular T (TBB n) := by
  apply regular_imp.mpr;
  contrapose!;
  intro _;
  exact singular_boxItr_bot;

@[simp, grind .]
lemma regular_fconj_TBB_finset {Γ : Finset ℕ} : Regular T (⋀(Γ.image TBB)) := by grind;

@[simp, grind .]
lemma singular_TBBMinus (hs : s.Finite) : Singular T (TBBMinus s) := by grind;

end LetterlessFormula


namespace LetterlessFormulaSet

variable {T₀ T U : FirstOrder.ArithmeticTheory} [T.Δ₁] {A B : LetterlessFormula}
variable {X : LetterlessFormulaSet} {A : LetterlessFormula} {s : Set ℕ}

@[grind] def spectrum (X : LetterlessFormulaSet) : Set ℕ := ⋂ A ∈ X, LetterlessFormula.spectrum A
@[grind] def trace (X : LetterlessFormulaSet) : Set ℕ := X.spectrumᶜ

@[grind] def Regular (T : FirstOrder.ArithmeticTheory) [T.Δ₁] (X : LetterlessFormulaSet) := ∀ A : LetterlessFormula, A ∈ X → LetterlessFormula.Regular T A
@[grind] def Singular (T : FirstOrder.ArithmeticTheory) [T.Δ₁] (X : LetterlessFormulaSet) := ¬(X.Regular T)

lemma eq_spectrum : X.spectrum = (⋂ A ∈ X, LetterlessFormula.spectrum A) := rfl
lemma eq_trace : X.trace = (⋃ A ∈ X, LetterlessFormula.trace A) := by simp [LetterlessFormulaSet.trace, LetterlessFormula.trace, spectrum];

@[grind =]
lemma iff_singular_exists_singular : X.Singular T ↔ ∃ (A : LetterlessFormula), A ∈ X ∧ LetterlessFormula.Singular T A := by grind;

lemma spectrum_subset_of_mem (h : A ∈ X) : X.spectrum ⊆ A.spectrum := by
  intro i hi;
  apply Set.mem_iInter.mp hi A;
  grind;

variable [ℕ↓[ℒₒᵣ] ⊧* T]

@[simp, grind =_]
lemma eq_trace_singleton : trace {A} = LetterlessFormula.trace A := by
  rw [eq_trace];
  simp;

lemma eq_TBB_trace : LetterlessFormulaSet.trace (TBB '' s) = s := by simp [eq_trace]

@[simp, grind =_]
lemma eq_trace_TBB_trace : LetterlessFormulaSet.trace (TBB '' X.trace) = X.trace := eq_TBB_trace

@[simp, grind .]
lemma regular_TBB_set {X : Set ℕ} : LetterlessFormulaSet.Regular T (X.image TBB) := by grind;

@[simp, grind =_]
lemma eq_trace_TBBMinus_singleton (hs : s.Finite) : trace {TBBMinus s} = sᶜ := by grind [eq_trace_singleton];

@[simp, grind .]
lemma singular_TBBMinus_singleton (hs : s.Finite) : LetterlessFormulaSet.Singular T {TBBMinus _ hs} := by
  simp only [Singular, Regular, Set.mem_singleton_iff, forall_eq];
  grind;

end LetterlessFormulaSet


section

open LetterlessFormula (interpret)

variable
  {T₀ T : FirstOrder.ArithmeticTheory} [ℕ↓[ℒₒᵣ] ⊧* T] [T.Δ₁] [𝗜𝚺₁ ⪯ T]
  {A B : LetterlessFormula}

/-- **Letterless arithmetical completeness**: for a `Σ₁`-sound `Δ₁` extension `T` of
`𝗜𝚺₁`, a letterless formula belongs to `GL` iff its interpretation under the standard
provability predicate is provable in `T`. -/
lemma letterless_arithmetical_completeness : A ∈ LogicGL ↔ T ⊢ A.interpret T.standardProvability := by
  have hlift : (LetterlessFormula.lift A : Formula Empty) = A := by
    induction A with
    | atom a => exact a.elim
    | _ => simp_all [LetterlessFormula.lift];
  rw [LogicGL.arithmetical_completeness_iff_of_sigma1_sound (T := T)];
  constructor;
  . intro h;
    have := h (⟨Empty.elim⟩ : Realization Empty ℒₒᵣ);
    rw [← hlift] at this;
    simpa only [LetterlessFormula.interpret_lift] using this;
  . intro h f;
    rw [← hlift];
    simpa only [LetterlessFormula.interpret_lift] using h;

namespace LetterlessFormula

@[grind →]
lemma iff_regular_of_provable_iff (h : A 🡘 B ∈ LogicGL) : A.Regular T ↔ B.Regular T := by
  have : T ⊢  interpret _ (A 🡘 B) := letterless_arithmetical_completeness (T := T) |>.mp h;
  have : ℕ↓[ℒₒᵣ] ⊧ interpret _ (A 🡘 B) := FirstOrder.ArithmeticTheory.SoundOn.sound (F := λ _ => True) this $ by simp;
  grind;

@[grind →]
lemma iff_singular_of_provable_iff (h : A 🡘 B ∈ LogicGL) : A.Singular T ↔ B.Singular T := by
  grind [iff_regular_of_provable_iff h];

@[grind =]
lemma iff_regular_trace_finite : A.Regular T ↔ (trace A).Finite := by
  constructor;
  . contrapose!;
    intro h;
    replace h : (spectrum A).Finite := by grind;
    apply iff_singular_of_provable_iff (LetterlessFormula.TBBMinus_normalization_of_finite_spectrum h) |>.mpr;
    grind;
  . intro h;
    apply iff_regular_of_provable_iff (LetterlessFormula.TBB_normalization_of_finite_trace h) |>.mpr;
    grind;

@[grind <=]
lemma finite_spectrum_of_singular : A.Singular T → (A.spectrum).Finite := by grind;

end LetterlessFormula


namespace LetterlessFormulaSet

variable {X : LetterlessFormulaSet} {A : LetterlessFormula}

@[grind <=]
lemma trace_cofinite_of_singular (h : X.Singular T) : X.traceᶜ.Finite := by
  obtain ⟨A, hA, h⟩ := iff_singular_exists_singular.mp h;
  suffices X.spectrum ⊆ A.spectrum by
    simp only [LetterlessFormulaSet.trace, compl_compl];
    apply Set.Finite.subset ?_ this;
    grind;
  grind [spectrum_subset_of_mem];

end LetterlessFormulaSet

section

variable {α} {X Y : LetterlessFormulaSet} {A : LetterlessFormula}

/--
  Finite compactness for quasi-normal extensions of `GL` by lifted letterless formula
  sets, for an arbitrary formula `B`: a provable formula follows in `GL` from the
  lifted conjunction of a finite subset of the letterless axioms.
-/
lemma GL_sumQuasiNormal_finite_provable {X : LetterlessFormulaSet} {B : Formula α}
    (hB : B ∈ ((@LogicGL α) +ᴸ ↑X)) :
    ∃ Y : LetterlessFormulaFinset, (∀ C ∈ Y, C ∈ X) ∧
      ((LetterlessFormula.lift (⋀Y) : Formula α) 🡒 B) ∈ LogicGL := by
  induction hB with
    | mem₁ hB =>
      exact ⟨∅, by simp, by simpa using ProvableHilbert.af hB⟩;
    | mem₂ hB =>
      obtain ⟨C, hC, rfl⟩ := hB;
      exact ⟨{C}, by simp only [Finset.mem_singleton]; rintro D rfl; exact hC,
        by rw [show ((⋀({C} : LetterlessFormulaFinset)) : LetterlessFormula) = C by simp];
           exact ProvableHilbert.impId⟩;
    | mdp _ _ ih₁ ih₂ =>
      obtain ⟨Y₁, hY₁, h₁⟩ := ih₁;
      obtain ⟨Y₂, hY₂, h₂⟩ := ih₂;
      use Y₁ ∪ Y₂, by grind;
      have w₁ : (⊢ʰ[GL] ((LetterlessFormula.lift (⋀(Y₁ ∪ Y₂)) : Formula α) 🡒 LetterlessFormula.lift (⋀Y₁))) := by
        simpa [LetterlessFormula.lift] using ProvableHilbert.lift (α := α)
          (ProvableHilbert.imp_fconj_fconj_of_subset Finset.subset_union_left);
      have w₂ : (⊢ʰ[GL] ((LetterlessFormula.lift (⋀(Y₁ ∪ Y₂)) : Formula α) 🡒 LetterlessFormula.lift (⋀Y₂))) := by
        simpa [LetterlessFormula.lift] using ProvableHilbert.lift (α := α)
          (ProvableHilbert.imp_fconj_fconj_of_subset Finset.subset_union_right);
      exact ProvableHilbert.mdp
        (ProvableHilbert.mdp ProvableHilbert.prop2 (ProvableHilbert.impTrans w₁ h₁))
        (ProvableHilbert.impTrans w₂ h₂);
    | @subst B s _ ih =>
      obtain ⟨Y, hY, hGL⟩ := ih;
      use Y, hY;
      have := ProvableHilbert.subst (s := s) hGL;
      simpa [LetterlessFormula.subst_lift] using this;
/--
  The converse: any formula that follows in `GL` from the lifted conjunction of a
  finite subset of the letterless axioms belongs to the quasi-normal extension.
-/
lemma GL_sumQuasiNormal_of_finite_provable {X : LetterlessFormulaSet} {B : Formula α}
    {Y : LetterlessFormulaFinset} (hY : ∀ C ∈ Y, C ∈ X)
    (hGL : ((LetterlessFormula.lift (⋀Y) : Formula α) 🡒 B) ∈ LogicGL) :
    B ∈ ((@LogicGL α) +ᴸ ↑X) := by
  have h₂ : (LetterlessFormula.lift (⋀Y) : Formula α) ∈ ((@LogicGL α) +ᴸ ↑X) := by
    suffices ∀ Γ : LetterlessFormulaList, (∀ C ∈ Γ, C ∈ X) →
        (LetterlessFormula.lift (⋀Γ) : Formula α) ∈ ((@LogicGL α) +ᴸ ↑X) by
      exact this Y.toList (fun C hC => hY C (Finset.mem_toList.mp hC));
    intro Γ;
    induction Γ with
    | nil =>
      intro _;
      exact Logic.sumQuasiNormal.mem₁ (ProvableHilbert.impId (A := (⊥ : Formula α)));
    | cons C Γ ih =>
      intro hΓ;
      rcases Γ with _ | ⟨D, Γ⟩;
      . exact Logic.sumQuasiNormal.mem₂ ⟨C, hΓ C (by simp), rfl⟩;
      . have hC : (LetterlessFormula.lift C : Formula α) ∈ ((@LogicGL α) +ᴸ ↑X) :=
          Logic.sumQuasiNormal.mem₂ ⟨C, hΓ C (by simp), rfl⟩;
        have hrest : (LetterlessFormula.lift (⋀(D :: Γ)) : Formula α) ∈ ((@LogicGL α) +ᴸ ↑X) :=
          ih (fun E hE => hΓ E (by grind));
        have heq : (LetterlessFormula.lift (⋀(C :: D :: Γ)) : Formula α)
            = (LetterlessFormula.lift C : Formula α) ⋏ (LetterlessFormula.lift (⋀(D :: Γ))) := by
          simp [FormulaList.conj, Formula.and];
        rw [heq];
        exact Logic.sumQuasiNormal.mdp
          (Logic.sumQuasiNormal.mdp (Logic.sumQuasiNormal.mem₁ ProvableHilbert.andIntro) hC) hrest;
  exact Logic.sumQuasiNormal.mdp (Logic.sumQuasiNormal.mem₁ hGL) h₂;

/--
  Compactness for quasi-normal extensions of `GL` by (lifted) letterless formula sets:
  a lifted letterless formula is provable iff it follows from a finite subset in `GL`
  (cf. `Logic.sumQuasiNormal.iff_provable_finite_provable` in Foundation).
-/
lemma iff_GL_sumQuasiNormal_provable_finite_provable {X : LetterlessFormulaSet} {A : LetterlessFormula} :
    ↑A ∈ ((@LogicGL α) +ᴸ ↑X) ↔
    ∃ Y : LetterlessFormulaFinset, (∀ B ∈ Y, B ∈ X) ∧ ((⋀Y) 🡒 A) ∈ LogicGL := by
  constructor;
  . intro h;
    obtain ⟨Y, hY, hGL⟩ := GL_sumQuasiNormal_finite_provable h;
    exact ⟨Y, hY, iff_lift_mem_LogicGL.mp (by simpa [LetterlessFormula.lift] using hGL)⟩;
  . rintro ⟨Y, hY, hGL⟩;
    apply GL_sumQuasiNormal_of_finite_provable hY;
    have h₁ : (LetterlessFormula.lift ((⋀Y) 🡒 A) : Formula α) ∈ LogicGL :=
      ProvableHilbert.lift hGL;
    rwa [show LetterlessFormula.lift ((⋀Y) 🡒 A)
      = ((LetterlessFormula.lift (⋀Y) : Formula α) 🡒 LetterlessFormula.lift A) from rfl] at h₁;

lemma iff_GL_sumQuasiNormal_proves_subset_spectrum (hSR : X.Singular T ∨ A.Regular T)
  : ↑A ∈ ((@LogicGL α) +ᴸ X) ↔ X.spectrum ⊆ A.spectrum := by calc
  _ ↔ ∃ Y : LetterlessFormulaFinset, (∀ B ∈ Y, B ∈ X) ∧ (⋀Y) 🡒 A ∈ LogicGL := by
    exact iff_GL_sumQuasiNormal_provable_finite_provable;
  _ ↔ ∃ Y : LetterlessFormulaFinset, (∀ B ∈ Y, B ∈ X) ∧ (⋂ B ∈ Y, spectrum B) ⊆ A.spectrum := by
    constructor;
    . rintro ⟨Y, hY, h⟩;
      use Y;
      constructor;
      . assumption;
      . replace h := iff_GL_proves_imp_GL_subset_spectrum.mp h;
        simp_all [LetterlessFormula.spectrum_fconj];
    . rintro ⟨Y, hY, h⟩;
      use Y;
      constructor;
      . assumption;
      . apply iff_GL_proves_imp_GL_subset_spectrum.mpr;
        simp_all [LetterlessFormula.spectrum_fconj];
  _ ↔ (⋂ B ∈ X, spectrum B) ⊆ A.spectrum := by
    constructor;
    . rintro ⟨Y, hY, h⟩ i hi;
      apply h;
      simp_all;
    . intro h;
      rcases hSR with X_singualr | A_regular;
      . wlog X_infinite : X.Infinite;
        . replace X_infinite : X.Finite := by simpa using X_infinite;
          use X_infinite.toFinset;
          constructor;
          . simp;
          . intro i hi;
            apply h;
            simp_all;
        obtain ⟨B, hBX, B_singular⟩ := LetterlessFormulaSet.iff_singular_exists_singular.mp X_singualr;
        obtain ⟨f, f0, f_ss, fX, f_inv⟩ := Set.infinitely_finset_approximate X.to_countable X_infinite hBX;
        have f_mono : Monotone f := monotone_nat_of_le_succ (fun i => (f_ss i).1);
        let sf : ℕ → Set ℕ := fun i => ⋂ C ∈ f i, spectrum C;
        have sf_anti : ∀ i j, i ≤ j → sf j ⊆ sf i := by
          intro i j hij n hn;
          simp only [sf, Set.mem_iInter] at hn ⊢;
          intro C hC;
          exact hn C (f_mono hij hC);
        have sf0 : sf 0 = spectrum B := by simp [sf, f0];
        have sf_finite : ∀ i, (sf i).Finite := by
          intro i;
          apply Set.Finite.subset (s := sf 0) ?_ (sf_anti 0 i (Nat.zero_le i));
          rw [sf0];
          exact LetterlessFormula.finite_spectrum_of_singular B_singular;
        have sf_X : ∀ i, (⋂ D ∈ X, spectrum D) ⊆ sf i := by
          intro i n hn;
          simp only [sf, Set.mem_iInter] at hn ⊢;
          intro C hC;
          exact hn C (fX i hC);
        obtain ⟨k, hk⟩ : ∃ k, sf k = ⋂ D ∈ X, spectrum D := by
          by_contra! hne;
          apply Finset.no_ssubset_descending_chain (f := fun i => (sf_finite i).toFinset);
          intro i;
          have hss : (⋂ D ∈ X, spectrum D) ⊂ sf i := Set.ssubset_of_subset_ne (sf_X i) (Ne.symm (hne i));
          obtain ⟨n, hn₁, hn₂⟩ := (Set.ssubset_iff_of_subset hss.subset).mp hss;
          obtain ⟨C, hC₁, hC₂⟩ : ∃ C ∈ X, n ∉ spectrum C := by
            by_contra hcon;
            push Not at hcon;
            exact hn₂ (Set.mem_iInter₂.mpr hcon);
          obtain ⟨j, hj⟩ := f_inv C hC₁;
          have hij : i < j := by
            by_contra hle;
            push Not at hle;
            have hsub : sf i ⊆ spectrum C := by
              intro m hm;
              exact Set.mem_iInter₂.mp (sf_anti j i hle hm) C hj;
            exact hC₂ (hsub hn₁);
          use j, hij;
          rw [Set.Finite.toFinset_ssubset_toFinset];
          apply Set.ssubset_of_subset_ne (sf_anti i j hij.le);
          intro heq;
          have hnj : n ∈ sf j := heq ▸ hn₁;
          exact hC₂ (Set.mem_iInter₂.mp hnj C hj);
        refine ⟨f k, ?_, ?_⟩;
        · intro D hD; exact fX k hD;
        · show (⋂ D ∈ f k, spectrum D) ⊆ A.spectrum;
          rw [(show (⋂ D ∈ f k, spectrum D) = sf k from rfl), hk];
          exact h;
      . have htr : (trace A).Finite := LetterlessFormula.iff_regular_trace_finite.mp A_regular;
        have H : ∀ i ∈ trace A, ∃ B, ∃ _ : B ∈ X, i ∈ trace B := by
          have hcov : trace A ⊆ ⋃ B ∈ X, trace B := by
            apply Set.compl_subset_compl.mp;
            simp only [LetterlessFormula.trace, Set.compl_iUnion, compl_compl];
            exact h;
          simpa [Set.subset_def] using hcov;
        let cf := λ i (hi : i ∈ trace A) => (H i hi).choose;
        have cf_in_X : ∀ {i} {hi : i ∈ trace A}, (cf i hi) ∈ X := by
          intro i hi; exact (H i hi).choose_spec.1;
        have H₂ : ⋂ i ∈ trace A, spectrum (cf i (by assumption)) ⊆ A.spectrum := by
          suffices trace A ⊆ ⋃ i ∈ trace A, trace (cf i (by assumption)) by
            apply Set.compl_subset_compl.mp;
            simpa [LetterlessFormula.trace];
          intro j hj;
          simp only [Set.mem_iUnion, cf];
          exact ⟨j, hj, (H j hj).choose_spec.2⟩;
        have : Fintype { i // i ∈ trace A } := htr.fintype;
        use Finset.univ.image (λ i : { i // i ∈ trace A } => cf i.1 i.2);
        constructor;
        . simp only [Finset.mem_image, Finset.mem_univ, true_and, Subtype.exists, forall_exists_index];
          rintro B i hi rfl;
          exact (H i hi).choose_spec.1;
        . intro n hn;
          apply H₂;
          simp only [Finset.mem_image, Finset.mem_univ, true_and, Subtype.exists, Set.iInter_exists, Set.mem_iInter] at hn ⊢;
          intro j hj;
          exact hn (cf j hj) j hj rfl;

lemma iff_subset_sumQuasiNormal_subset_spectrum (hSR : X.Regular T ∨ Y.Singular T)
  : ((@LogicGL α) +ᴸ X) ⊆ ((@LogicGL α) +ᴸ Y) ↔ Y.spectrum ⊆ X.spectrum := by calc
  -- _ ↔ ∀ A ∈ Y, A ∈ ((LogicGL) +ᴸ Y) → A ∈ ((LogicGL) +ᴸ X) := by grind;
  _ ↔ ∀ (A : LetterlessFormula), A ∈ X → ↑A ∈ ((@LogicGL α) +ᴸ Y) := by
    rw [Logic.sumQuasiNormal.iff_subset];
    constructor;
    . intro h A hA;
      apply @h A (Set.mem_image_of_mem _ hA);
    . intro h;
      rintro B ⟨A, hA, rfl⟩;
      exact h A hA;
  _ ↔ ∀ (A : LetterlessFormula), A ∈ X → Y.spectrum ⊆ A.spectrum := by
    constructor;
    . intro h A hA;
      apply iff_GL_sumQuasiNormal_proves_subset_spectrum (α := α) (T := T) (by grind) |>.mp
      grind;
    . intro h A hA;
      apply iff_GL_sumQuasiNormal_proves_subset_spectrum (α := α) (T := T) (by grind) |>.mpr;
      grind;
  _ ↔ Y.spectrum ⊆ (⋂ A ∈ X, spectrum A) := by
    simp;

lemma iff_subset_sumQuasiNormal_subset_trace (hSR : X.Regular T ∨ Y.Singular T)
  : ((@LogicGL α) +ᴸ X) ⊆ ((@LogicGL α) +ᴸ Y) ↔ X.trace ⊆ Y.trace := by
  apply Iff.trans $ iff_subset_sumQuasiNormal_subset_spectrum (α := α) hSR;
  simp [LetterlessFormulaSet.trace];

lemma iff_eq_sumQuasiNormal_eq_spectrum (hSR : (X.Singular T ∧ Y.Singular T) ∨ (X.Regular T ∧ Y.Regular T))
  : ((@LogicGL α) +ᴸ X) = ((@LogicGL α) +ᴸ Y) ↔ X.spectrum = Y.spectrum := by
  grind [
    Set.Subset.antisymm_iff,
    iff_subset_sumQuasiNormal_subset_spectrum (α := α) (T := T) (X := X) (Y := Y) (by tauto),
    iff_subset_sumQuasiNormal_subset_spectrum (α := α) (T := T) (X := Y) (Y := X) (by tauto)
  ];

lemma iff_eq_sumQuasiNormal_eq_trace (hSR : (X.Singular T ∧ Y.Singular T) ∨ (X.Regular T ∧ Y.Regular T))
  : ((@LogicGL α) +ᴸ X) = ((@LogicGL α) +ᴸ Y) ↔ X.trace = Y.trace := by
  apply Iff.trans $ iff_eq_sumQuasiNormal_eq_spectrum (α := α) hSR;
  simp [LetterlessFormulaSet.trace];

namespace FormulaSet

def Letterless {α} (X : FormulaSet α) : Prop := ∀ A ∈ X, A.Letterless

end FormulaSet


namespace LetterlessFormula

@[simp, grind =] lemma eq_lift_TBB {n : ℕ} : lift (α := α) (TBB n) = TBB n := by grind;

end LetterlessFormula


namespace LetterlessFormulaSet

@[simp, grind =]
lemma eq_lift_TBB_set {X : Set ℕ} : lift (α := α) (TBB '' X) = TBB '' X := by
  ext A;
  constructor;
  . rintro ⟨A, hA, rfl⟩; grind;
  . rintro ⟨i, hi, rfl⟩; grind [LetterlessFormulaSet.lift];

end LetterlessFormulaSet


lemma eq_letterless_GL_quasiNormal_extension_GLAlpha_of_regular (X_regular : X.Regular T)
  : LogicGLAlpha X.trace = ((@LogicGL α) +ᴸ ↑X) := by
  apply iff_eq_sumQuasiNormal_eq_trace (T := T) (by grind) |>.mpr;
  grind;

lemma eq_letterless_GL_quasiNormal_extension_GLBetaMinus_of_singular [DecidableEq α] (X_singular : X.Singular T)
  : LogicGLBetaMinus X.trace = ((@LogicGL α) +ᴸ ↑X) := by
  apply iff_eq_sumQuasiNormal_eq_trace (T := T) (by grind) |>.mpr;
  rw [LetterlessFormulaSet.eq_trace_TBBMinus_singleton (by grind)];
  grind;

/--
  Quasi-normal `GL` extension by letterless formula set `X` is
  either `LogicGLAlpha X.trace` (when `X` is regular, so `X.trace` is finite) or `LogicGLBetaMinus X.trace` (when `X` is singular, so `X.trace` is cofinite)
-/
theorem classification_letterless_quasiNormal_GL_extension [DecidableEq α] :
  (∃ _ : X.Regular T, ((@LogicGL α) +ᴸ ↑X) = LogicGLAlpha X.trace) ∨
  (∃ _ : X.Singular T, ((@LogicGL α) +ᴸ ↑X) = LogicGLBetaMinus X.trace) := by
  by_cases h : X.Regular T;
  . left;
    exact ⟨h, eq_letterless_GL_quasiNormal_extension_GLAlpha_of_regular h |>.symm⟩;
  . right;
    exact ⟨h, eq_letterless_GL_quasiNormal_extension_GLBetaMinus_of_singular h |>.symm⟩;

end

section

variable {T : FirstOrder.ArithmeticTheory} [𝗜𝚺₁ ⪯ T] [T.Δ₁] [ℕ↓[ℒₒᵣ] ⊧* T]

lemma lconj_mem_sumQuasiNormal {α : Type*} {Z : Logic α} {Γ : FormulaList α}
    (h : ∀ B ∈ Γ, B ∈ (LogicGL +ᴸ Z)) : (⋀Γ) ∈ (LogicGL +ᴸ Z) := by
  match Γ with
  | [] => exact Logic.sumQuasiNormal.mem₁ ProvableHilbert.top
  | [B] => simpa using h B (by simp)
  | B :: C :: Γ =>
    have hB := h B (by simp)
    have hrest := lconj_mem_sumQuasiNormal (Γ := C :: Γ) (fun D hD => h D (by simp only [List.mem_cons] at hD ⊢; tauto))
    show (B ⋏ ⋀(C :: Γ)) ∈ (LogicGL +ᴸ Z)
    exact Logic.sumQuasiNormal.mdp (Logic.sumQuasiNormal.mdp (Logic.sumQuasiNormal.mem₁ ProvableHilbert.andIntro) hB) hrest

lemma fconj_mem_sumQuasiNormal {α : Type*} {Z : Logic α} {Γ : FormulaFinset α}
    (h : ∀ B ∈ Γ, B ∈ (LogicGL +ᴸ Z)) : (⋀Γ) ∈ (LogicGL +ᴸ Z) := by
  show (FormulaList.conj Γ.toList) ∈ (LogicGL +ᴸ Z)
  apply lconj_mem_sumQuasiNormal
  intro B hB
  exact h B (Finset.mem_toList.mp hB)

omit [ℕ↓[ℒₒᵣ] ⊧* T] in
open Classical in
theorem letterless_provabilityLogic (X : LetterlessFormulaSet) :
  ((@LogicGL α) +ᴸ ↑X) = T.provabilityLogicRelativeTo (T ∪ (X.image (LetterlessFormula.standardInterpret T))) := by
  ext A;
  simp [FirstOrder.ArithmeticTheory.provabilityLogicRelativeTo];
  constructor;
  . intro h;
    induction h with
    | mem₁ hA => intro f; exact Entailment.WeakerThan.pbl (LogicGL.arithmetical_soundness' hA)
    | @mem₂ B hB =>
      intro f;
      obtain ⟨C, hC, rfl⟩ := hB;
      simp only [LetterlessFormula.interpret_lift];
      apply Entailment.by_axm;
      simp only [Set.mem_union];
      exact Or.inr ⟨C, hC, rfl⟩;
    | @mdp B C _ _ ihBC ihB => intro f; exact (ihBC f) ⨀ (ihB f)
    | @subst B s _ ihB => intro f; simp only [Formula.interpret_subst]; exact ihB _
  . intro h;
    let f₀ := LogicGL.uniformRealization (α := α) T;
    obtain ⟨⟨s, hs_sub⟩, hs⟩ := FFL.FirstOrder.Theory.compact_add_right (h f₀);
    obtain ⟨Δ, hΔ_sub, hΔ_cov⟩ := finite_preimage_choice s X (LetterlessFormula.standardInterpret T) (by
      intro σ hσ;
      obtain ⟨B, hB, hσ'⟩ := hs_sub hσ;
      exact ⟨B, hB, hσ'⟩);
    set C : Formula α := ⋀ (Δ.image LetterlessFormula.lift) with hC;
    have ha : (C 🡒 A) ∈ LogicGL := by
      apply (LogicGL.uniformRealization_spec (T := T) (C 🡒 A)).mp;
      show T ⊢ f₀ T C 🡒 f₀ T A;
      apply Entailment.C_trans ?_ hs;
      apply Entailment.right_Fconj_intro;
      intro σ hσ;
      obtain ⟨B, hBΔ, rfl⟩ := hΔ_cov σ hσ;
      rw [show (LetterlessFormula.standardInterpret T B)
        = f₀ T (LetterlessFormula.lift B : Formula α) from
        (LetterlessFormula.interpret_lift (f := f₀)).symm];
      have hmem : (C 🡒 LetterlessFormula.lift B) ∈ LogicGL := by
        show ⊢ʰ[GL] (C 🡒 LetterlessFormula.lift B);
        have hsub : ({LetterlessFormula.lift B} : FormulaFinset α) ⊆ Δ.image LetterlessFormula.lift :=
          Finset.singleton_subset_iff.mpr (Finset.mem_image_of_mem _ hBΔ);
        simpa using ProvableHilbert.imp_fconj_fconj_of_subset (Γ := Δ.image LetterlessFormula.lift) hsub;
      exact LogicGL.arithmetical_soundness' hmem;
    have hb : C ∈ ((@LogicGL α) +ᴸ X.lift) := by
      apply fconj_mem_sumQuasiNormal;
      intro B hB;
      obtain ⟨C, hCΔ, rfl⟩ := Finset.mem_image.mp hB;
      exact Logic.sumQuasiNormal.mem₂ (Set.mem_image_of_mem _ (hΔ_sub hCΔ));
    exact Logic.sumQuasiNormal.mdp (Logic.sumQuasiNormal.mem₁ ha) hb;

omit [ℕ↓[ℒₒᵣ] ⊧* T] in
theorem LogicGLAlpha.eq_provabilityLogicRelativeTo {X : Set ℕ}
  : LogicGLAlpha (α := α) X = T.provabilityLogicRelativeTo (T ∪ (X.image (λ i => LetterlessFormula.standardInterpret T (TBB i)))) := by
  suffices (LetterlessFormula.standardInterpret T '' TBB '' X) = (X.image (λ i => LetterlessFormula.standardInterpret T (TBB i))) by
    exact this ▸ (letterless_provabilityLogic (X := X.image TBB));
  ext i;
  simp;

omit [ℕ↓[ℒₒᵣ] ⊧* T] in
theorem LogicA.eq_provabilityLogicRelativeTo
  : LogicGLAlpha (α := α) Set.univ = T.provabilityLogicRelativeTo (T ∪ (Set.univ.image (λ i => LetterlessFormula.standardInterpret T (TBB i)))) :=
  LogicGLAlpha.eq_provabilityLogicRelativeTo

end

end

end
